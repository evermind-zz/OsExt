plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.github.evermindzz.osext'
    compileSdk 31

    defaultConfig {
        minSdk 19
        targetSdk 31
        versionCode 1
        versionName "1.0.0"

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-19"
                // https://developer.android.com/ndk/guides/cmake.html
            }
        }

        ndkVersion "25.2.9519653"
        ndk {
            // https://developer.android.com/ndk/guides/abis.html#sa
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    splits {
        abi {
            enable false
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    androidComponents {
        onVariants(selector().withBuildType("release"), {
            packaging.jniLibs.excludes.add('**/libosexttest.so')  // <- this is only for testing the exceptions
        })
    }
}

dependencies {
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.runner
    androidTestImplementation libs.hamcrest.lib
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = project.group
                artifactId = 'osext-stat'
                version = android.defaultConfig.versionName
            }
        }
    }
}
